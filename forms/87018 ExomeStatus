Private sqlSelectFrom As String
Private excludeReferrals As String

Private Sub Form_Load()
    'So doesnt show records on form open
    Dim sqlNull As String
    
    'The fields and tables used for the subform recordsource, combined with various WHERE clauses below
    sqlSelectFrom = "SELECT NGSTest.NGSTestID, dbo_Patient_table.PatientTrustID, dbo_Patient_table.LastName, dbo_Patient_table.FirstName, NGSTest.DateRequested, Status.StatusID, Status.Status, Checker.Name As RefName, NGSWESBatch.BatchName " & _
    "FROM ((((NGSTest INNER JOIN Patients ON NGSTest.InternalPatientID = Patients.InternalPatientID) INNER JOIN dbo_Patient_table ON Patients.PatientID = dbo_Patient_table.PatientTrustID) INNER JOIN Checker ON NGSTest.BookBy = Checker.Check1ID) INNER JOIN Status ON NGSTest.StatusID = Status.StatusID) LEFT JOIN NGSWESBatch ON NGSTest.WESBatch = NGSWESBatch.NGSWESBatchID "
    'String of referral types to exlude (e.g. Oncology samples) to be inserted into WHERE clause of recordsource
    excludeReferrals = "NGSTest.ReferralID <> 1199901217"
    
    sqlNull = "SELECT Null AS NGSTestID, Null AS PatientTrustID, Null AS LastName, Null AS FirstName, Null AS DateRequested, Null AS StatusID, Null AS Status, Null AS RefName, Null AS BatchName;"
    [Form_s87018_ExomeStatus].RecordSource = sqlNull
End Sub

Private Sub Search_Click()
    Dim sqlSearchPatient As String
    'Set subform RecordSet to retrieve all WES tests that match PRU number
    If Not IsNull(Me![PatPRU]) Then
        sqlSearchPatient = sqlSelectFrom & "WHERE " & excludeReferrals & " And (((Patients.PatientID) Like [Forms]![87018 ExomeStatus].[PatPRU] & '*')) ORDER BY NGSTest.DateRequested;"
        [Form_s87018_ExomeStatus].Form.RecordSource = sqlSearchPatient
    End If
End Sub

Private Sub AllInProgress_Click()
    Dim sqlInProgress As String
    'Set subform RecordSet to retrieve all in progress WES tests
    sqlInProgress = sqlSelectFrom & "WHERE " & excludeReferrals & " And Status.StatusID <> 2 And Status.StatusID <> 4 And Status.StatusID <> 5 And Status.StatusID <> 1202218787 ORDER BY NGSTest.DateRequested;"
    [Form_s87018_ExomeStatus].Form.RecordSource = sqlInProgress
End Sub

Private Sub AllApproval_Click()
    Dim sqlApproval As String
    'Set subform RecordSet to retrieve all in progress WES tests
    sqlApproval = sqlSelectFrom & "WHERE " & excludeReferrals & " And Status.StatusID = 1202218801 ORDER BY NGSTest.DateRequested;"
    [Form_s87018_ExomeStatus].Form.RecordSource = sqlApproval
End Sub

Private Sub Form_Unload(Cancel As Integer)
    'Reset to null query so form doesn't try to load records next time form is opened
    Dim sqlNull As String
    sqlNull = "SELECT Null AS NGSTestID, Null AS PatientTrustID, Null AS LastName, Null AS FirstName, Null AS DateRequested, Null AS StatusID, Null AS Status, Null AS RefName, Null AS BatchName;"
    [Form_s87018_ExomeStatus].RecordSource = sqlNull
End Sub

Private Sub Close_Click()
    DoCmd.Close
End Sub

