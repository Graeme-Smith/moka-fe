Option Compare Database

Private Sub PrimerSearch_Click()
' this script takes a genomic position as a search term and refreshes the subform filtering primers based on the search term

'define varaiable
Dim QRY As String ' to store the SQL
Dim search_chr As String ' the chromosome from search term
Dim search_pos As String ' the position from search term

' print the searchpos for debugging
Debug.Print [SearchPos]
' if no search term produce a message box
If IsNull([SearchPos]) Then
    MsgBox "please enter a coordinate"
Else

' split the input box into chromosome and position
' for chromosome remove chr if it was added but then add it incase it wasn't added- this enables X and Y to be queried
search_chr = "chr" & Replace(Split([SearchPos], ":", -1)(0), "Chr", "")

'take position
search_pos = Split([SearchPos], ":", -1)(1)

'MsgBox search_chr
'MsgBox search_pos

' build sql filtering on chromosome
QRY = "SELECT DISTINCT PrimerAmplicon.PrimerName, Chromosome.Chr, PrimerAmplicon.Start, PrimerAmplicon.Stop, PrimerLookup.Name AS GenomeBuild, PrimerTags.Name AS ForwardTag, dbo_PrimerTags_1.Name AS ReverseTag, PrimerAmplicon.Notes, PrimerAmplicon.ForwardSeq, PrimerAmplicon.ReverseSeq, PrimerAmplicon.TestResultNotes, PrimerAmplicon.Mix, PrimerAmplicon.FFreezer, PrimerAmplicon.FTray, PrimerAmplicon.FGrid, PrimerAmplicon.RFreezer, PrimerAmplicon.RTray, PrimerAmplicon.RGrid" & _
" FROM (Chromosome INNER JOIN ((PrimerTags INNER JOIN PrimerAmplicon ON PrimerTags.PrimerTagsID = PrimerAmplicon.FTag) INNER JOIN PrimerTags AS dbo_PrimerTags_1 ON PrimerAmplicon.RTag = dbo_PrimerTags_1.PrimerTagsID) ON Chromosome.ChrID = PrimerAmplicon.ChromosomeID) INNER JOIN PrimerLookup ON PrimerAmplicon.GenomeBuild = PrimerLookup.PrimerLookupID " & _
" WHERE Chromosome.Chr='" & search_chr & "' AND ([Start]+Len([ForwardSeq])+25) < " & search_pos & " AND ([Stop]-Len([ReverseSeq])-25) > " & search_pos & " AND PrimerAmplicon.Status=1202218832;"
' note status = tested in use, and position can't fall within the primer sequence or 25 bp from the primer

' print sql for debugging
Debug.Print QRY

' set subform record source as the new sql built above
[Form_s61002_FindPrimerByCoord].Form.RecordSource = QRY
End If
End Sub
