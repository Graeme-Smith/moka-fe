Private Function UpdateLog(FieldName As String, ValueNew As String)
    'Function to record updated value for a field in patient log
    Dim d As String
    Dim un As String
    Dim cn As String
    d = Format(Now(), "dd/mmm/yyyy Hh:Nn:ss")
    un = VBA.Environ("USERNAME")
    cn = VBA.Environ("COMPUTERNAME")
    patID = Me.Parent.InternalPatientID
    sqlPatLogVar = "INSERT INTO PatientLog (InternalPatientID, LogEntry, [Date], Login, PCName) " & _
                   "VALUES (" & patID & ", 'NGS: NGSVariantID " & Me.NGSVariantID & ": " & FieldName & " updated to " & ValueNew & "', #" & d & "#,'" & un & "','" & cn & "')"
    DoCmd.SetWarnings False
    DoCmd.RunSQL sqlPatLogVar
    DoCmd.SetWarnings True
End Function

Private Sub Gene_AfterUpdate()
    Dim hgnc_id As String
    Dim sql As String
    Dim rs As ADODB.Recordset
    Dim gene_list As String
    'Capture the HGNCID from column 1 of the dropdown and refresh to sync with database
    Me.HGNCID = Me.Gene.Column(1)
    Me.Refresh
    'Record update to gene in patient log.
    Call UpdateLog("Gene for transcript " & Me.Transcript, Me.Gene)
    'Top level variant table contains concatenation of gene symbols from each transcript. After gene is added/updated, re-generate this list
    'Pull out distinct list of genes for variant
    sql = "SELECT DISTINCT gene_symbol " & _
            "FROM ngsvariantannotations " & _
           "WHERE ngsvariantid = " & Me.NGSVariantID & " " & _
           "ORDER BY gene_symbol;"
    Set rs = New ADODB.Recordset
    rs.Open sql, CurrentProject.Connection, adOpenKeyset
    'Loop through the recordset and capture each gene symbol in semi-colon separated string
    gene_list = rs.Fields("GENE_SYMBOL")
    rs.MoveNext
    While Not rs.EOF
        gene_list = gene_list & ";" & rs.Fields("GENE_SYMBOL")
        rs.MoveNext
    Wend
    'Update the gene field on parent form and refresh to sync with database
    Me.Parent.Gene = gene_list
    Me.Parent.Refresh
End Sub

Private Sub HGVS_Protein_AfterUpdate()
    'Record update in patient log
    Call UpdateLog("HGVS p. for transcript " & Me.Transcript, Me.HGVS_Protein)
End Sub

Private Sub HGVS_Transcript_AfterUpdate()
    'Record update in patient log
    Call UpdateLog("HGVS c. for transcript " & Me.Transcript, Me.HGVS_Transcript)
End Sub

Private Sub Transcript_AfterUpdate()
    'Record update in patient log
    Call UpdateLog("Transcript", Me.Transcript)
End Sub
