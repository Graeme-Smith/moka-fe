Option Compare Database
Private internalPatID As String
Private ExomeTestID As String
Private ReferralID As String
Private Referral As String
Private save As Boolean

Private Sub Form_Open(Cancel As Integer)
    save = False
End Sub

Private Sub Form_Load()
    Dim openArgs() As String
    Dim patientID_PRU As String
    Dim username As String
    Dim todayDate As String
    Dim panelName As String
    Dim subpanelCode As String
    Dim extPanelRowSource As String

    'Assign default values
    Me![Category] = 692 'Clinician
    Me![SubCategory] = 693 'Candidate gene
    Me![Checker1] = 1201865468 'Clinician
    Me![CheckDate] = Format(Now(), "dd/mmm/yyyy Hh:Nn:ss")

    'Retrieve PatientID_PRU and ExomeTestID passed from ExomeTest form
    openArgs = Split(Me.openArgs, ", ")
    internalPatID = openArgs(0)
    patientID_PRU = openArgs(1)
    ExomeTestID = openArgs(2)
    ReferralID = openArgs(3)
    Referral = openArgs(4)

    'Set panel name
    username = VBA.Environ("USERNAME")
    todayDate = Format(Now(), "yymmdd")
    panelName = username & "_" & patientID_PRU & "_" & todayDate
    Me![Panel] = panelName

    'Set panel code
    Me![PanelCode] = "Pan" & Me![NGSPanelID]

    'Set extended panel options based on referral type
    If ReferralID = 1199901211 Then 'Renal
        subpanelCode = "68"
    ElseIf ReferralID = 1199901212 Then 'Skeletal
        subpanelCode = "68" '///////UPDATE TO CORRECT CODE WHEN SUBPANELS CREATED///////
    Else 'DevDel
        subpanelCode = "68" '///////UPDATE TO CORRECT CODE WHEN SUBPANELS CREATED///////
    End If
    extPanelRowSource = "SELECT NGSPanel.NGSPanelID AS Expr1, NGSPanel.PanelCode AS Expr2, NGSPanel.Panel AS Expr3 FROM NGSPanel WHERE NGSPanel.Subpanel = " & subpanelCode & " ORDER BY NGSPanel.[NGSPanelID];"
    ExtendedPanelCombo.RowSource = extPanelRowSource

End Sub

Private Sub addGene_Click()
    Dim sqlPanel As String
    Dim sqlGeneIns As String
    Dim rs As ADODB.Recordset

    If Not IsNull(Me![GeneSymbol]) Then
        'Check that gene hasn't already been added to panel
        sqlPanel = "SELECT * FROM NGSPanelGenes WHERE HGNCID = '" & Me![GeneSymbol].Column(1) & "' AND NGSPanelID = " & Me![NGSPanelID]
        Set rs = New ADODB.Recordset
        rs.Open sqlPanel, CurrentProject.Connection, adOpenKeyset
        If rs.EOF Then
            'Add selected gene to gene panel
            sqlGeneIns = "INSERT INTO NGSPanelGenes ( NGSPanelID, symbol, HGNCID) VALUES (" & Me![NGSPanelID] & ", '" & Me![GeneSymbol] & "', '" & Me![GeneSymbol].Column(1) & "')"
            DoCmd.SetWarnings False
            DoCmd.RunSQL sqlGeneIns
            DoCmd.SetWarnings True
            Me![GenePanelListBox].Value = Null
            Me![GenePanelListBox].Requery
            Me![GeneSymbol] = Null
        Else
            MsgBox "Gene has already been added to panel", vbInformation, "Gene Already Added"
        End If
    End If
    Me![GeneSymbol].SetFocus
End Sub

Private Sub GeneSymbol_NotInList(NewData As String, Response As Integer)
    'Replaces default error message if gene not in list
    MsgBox "Gene symbol not recognised. Please enter approved gene symbol from dropdown list.", vbExclamation, "Gene Not Found"
    Response = acDataErrContinue
    Me![GeneSymbol] = Null
End Sub

Private Sub HGNCLaunch_Click()
    Dim url As String
    Dim pathFireFox As String
    'Launch Phenotips in firefox
    url = "http://www.genenames.org/"
    pathFireFox = "C:\Program Files (x86)\Mozilla Firefox\firefox.exe"
    If Dir(pathFireFox) = "" Then
        pathFireFox = "C:\Program Files\Mozilla Firefox\firefox.exe"
    End If
    If Dir(pathFireFox) = "" Then
        MsgBox "FireFox path not found," & vbNewLine & "FireFox required to launch HGNC.", vbCritical, "ERROR"
        Exit Sub
    End If
    Shell """" & pathFireFox & """" & " -new-tab " & url, vbHide
End Sub

Private Sub removeGene_Click()
    Dim sqlDelGene As String
    'If no gene is selected, display error message
    If IsNull(Me![GenePanelListBox].Value) Then
        MsgBox "No gene selected.", vbExclamation, "No Gene Selected"
    'Delete gene from panel and update list box
    Else
        sqlDelGene = "DELETE FROM NGSPanelGenes WHERE NGSPanelGenesID = " & Me![GenePanelListBox].Value
        DoCmd.SetWarnings False
        DoCmd.RunSQL sqlDelGene
        DoCmd.SetWarnings True
        Me![GenePanelListBox].Value = Null
        Me![GenePanelListBox].Requery
    End If
    Me![GeneSymbol].SetFocus
End Sub

Private Sub Clear_Click()
    Dim sqlDelAll As String
    'Delete all genes from panel and update list box
    sqlDelAll = "DELETE FROM NGSPanelGenes WHERE NGSPanelID = " & Me![NGSPanelID]
    DoCmd.SetWarnings False
    DoCmd.RunSQL sqlDelAll
    DoCmd.SetWarnings True
    Me![GenePanelListBox].Value = Null
    Me![GenePanelListBox].Requery
    Me![GeneSymbol].SetFocus
End Sub

Private Sub ExtendedPanelCombo_NotInList(NewData As String, Response As Integer)
    'Replaces default error message if referal type not in list
    MsgBox "Gene panel not recognised. Please select from the dropdown list", vbExclamation, "Select Extended Gene Panel"
    Response = acDataErrContinue
    Me!ExtendedPanelCombo = Null
End Sub

Private Sub ExtendedPanelCombo_AfterUpdate()
    'Update extended panel code and name fields after selection
    Me![ExtendedPanelCode] = Me![ExtendedPanelCombo].Column(1)
    Me![ExtendedPanelName] = Me![ExtendedPanelCombo].Column(2)

    'Update Extended Panel listbox
    Me![ExtPanListbox].Requery
End Sub

Private Sub SubmitButton_Click()
    Dim sqlUpdatePan As String
    Dim sqlUpdateExt As String
    Dim sqlPatLog As String
    Dim d As String
    Dim un As String
    Dim cn As String

    'Check genes have been selected for analysis
    If Me![GenePanelListBox].ListCount = 0 And IsNull(Me![ExtendedPanelCombo]) Then
        MsgBox "No genes selected for analysis." & vbNewLine & "Please create a primary gene panel and/or select an extended panel before submitting.", vbExclamation, "No Genes Selected"
    Else
        'Add Primary NGSPanelID to ExomeTest record
        If Me![GenePanelListBox].ListCount <> 0 Then
            sqlUpdatePan = "UPDATE ExomeTest SET NGSPanelID = " & Me![NGSPanelID] & " WHERE ExomeTestID = " & ExomeTestID
            DoCmd.SetWarnings False
            DoCmd.RunSQL sqlUpdatePan
            DoCmd.SetWarnings True
        End If

        'Add Extended NGSPanelID to ExomeTest record
        If Not IsNull(Me![ExtendedPanelCombo]) Then
            sqlUpdateExt = "UPDATE ExomeTest SET ExtendedPanelID = " & Me![ExtendedPanelCombo] & " WHERE ExomeTestID = " & ExomeTestID
            DoCmd.SetWarnings False
            DoCmd.RunSQL sqlUpdateExt
            DoCmd.SetWarnings True
        End If

        'Change patient status from NGS Pending to NGS Pending
        sqlChangeStatus = "UPDATE Patients SET s_StatusOverall = 1202218799 WHERE InternalPatientID = " & internalPatID
        DoCmd.SetWarnings False
        DoCmd.RunSQL sqlChangeStatus
        DoCmd.SetWarnings True

        'Insert record of Exome test request and status change into patient log
        d = Format(Now(), "dd/mmm/yyyy Hh:Nn:ss")
        un = VBA.Environ("USERNAME")
        cn = VBA.Environ("COMPUTERNAME")
        sqlPatLogTest = "INSERT INTO PatientLog(InternalPatientID, LogEntry, [Date], Login, PCName) VALUES(" & internalPatID & ", 'NGS: " & Referral & " test request added. Main panel: " & Me![PanelCode] & ". Extended panel: " & Me![ExtendedPanelCode] & "',#" + d + "#,'" + un + "','" + cn + "')"
        sqlPatLogStatus = "INSERT INTO PatientLog(InternalPatientID, LogEntry, [Date], Login, PCName) VALUES(" & internalPatID & ", 'Patients: Status changed to NGS Pending ',#" + d + "#,'" + un + "','" + cn + "')"
        DoCmd.SetWarnings False
        DoCmd.RunSQL sqlPatLogTest
        DoCmd.RunSQL sqlPatLogStatus
        DoCmd.SetWarnings True

        'Display message and close form
        MsgBox "Exome test request submitted." & vbNewLine & vbNewLine & "Pending authorisation.", vbInformation, "Test Request Submitted"
        save = True
        DoCmd.Close
    End If
End Sub

Private Sub Cancel_Click()
    'Triggers Form_Unload procedure
    On Error GoTo ErrHandler
    DoCmd.Close
ErrHandler:
    If Err.Number = 2501 Then
        'Do nothing. Error indicates close was cancelled by user.
    End If
End Sub

Private Sub Form_Unload(Cancel As Integer)
    Dim sqlDelTest As String
    Dim sqlDelGenes As String
    Dim sqlDelPanel As String
    Dim sqlChangeStatus As String
    Dim sqlPatLogStatus As String
    Dim d As String
    Dim un As String
    Dim cn As String
    'Display warning message and delete created records if user chooses to exit without submitting
    If Not save Then
        If MsgBox("Are you sure you want to exit before submitting exome test request?" & vbNewLine & vbNewLine & "All changes will be lost", vbYesNo + vbExclamation, "Confirm Exit") = vbNo Then
            Cancel = True
        Else
            'Delete ExomeTest record
            sqlDelTest = "DELETE FROM ExomeTest WHERE ExomeTestID = " & ExomeTestID
            'Delete created gene panel + genes
            sqlDelGenes = "DELETE FROM NGSPanelGenes WHERE NGSPanelID = " & Me![NGSPanelID]
            sqlDelPanel = "DELETE FROM NGSPanel WHERE NGSPanelID = " & Me![NGSPanelID]
            DoCmd.SetWarnings False
            If Not IsNull(ExomeTestID) Then
                DoCmd.RunSQL sqlDelTest
            End If
            If Not IsNull(Me![NGSPanelID]) Then
                DoCmd.RunSQL sqlDelGenes
                DoCmd.RunSQL sqlDelPanel
            End If
            DoCmd.SetWarnings True
        End If
    End If
End Sub
