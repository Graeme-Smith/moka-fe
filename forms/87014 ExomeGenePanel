Option Compare Database
Private internalPatID As String
Private ExomeTestID As String
Private ReferralID As String
Private Referral As String
Private Sub Form_Load()
    Dim openArgs() As String
    Dim patientID_PRU As String
    Dim username As String
    Dim todayDate As String
    Dim panelName As String
    Dim sql1 As String

    'Assign default values
    Me![Category] = 692 'Clinician
    Me![SubCategory] = 693 'Candidate gene
    Me![Checker1] = 1201865468 'Clinician
    Me![CheckDate] = Format(Now(), "dd/mmm/yyyy Hh:Nn:ss")

    'Retrieve PatientID_PRU and ExomeTestID passed from ExomeTest form
    openArgs = Split(Me.openArgs, ", ")
    internalPatID = openArgs(0)
    patientID_PRU = openArgs(1)
    ExomeTestID = openArgs(2)
    ReferralID = openArgs(3)
    Referral = openArgs(4)

    'Set panel name
    username = VBA.Environ("USERNAME")
    todayDate = Format(Now(), "yymmdd")
    panelName = username & "_" & patientID_PRU & "_" & todayDate
    Me![Panel] = panelName

    'Set panel code
    Me![PanelCode] = "Pan" & Me![NGSPanelID]

    'Add NGSPanelID to ExomeTest record
    sql1 = "UPDATE ExomeTest SET NGSPanelID = " & Me![NGSPanelID] & " WHERE ExomeTestID = " & ExomeTestID
    DoCmd.SetWarnings False
    DoCmd.RunSQL sql1
    DoCmd.SetWarnings True
End Sub

Private Sub addGene_Click()
    Dim rs As ADODB.Recordset
    Dim sql2 As String
    Dim sql3 As String

    'Retrieve HGNCID for gene symbol entered
    Set rs = New ADODB.Recordset
    sql2 = "SELECT HGNCID FROM GenesHGNC_150205 WHERE ApprovedSymbol = '" & Me![GeneSymbol] & "'"
    rs.Open sql2, CurrentProject.Connection, adOpenKeyset

    'If gene symbol not found in approved HGNC list, display error message
    If rs.EOF Then
        MsgBox "Gene symbol not found in approved list. Could not add to panel.", vbExclamation, "Gene Not Found"
    'If gene symbol found in approved list, add gene to panel and display in list box.
    ElseIf rs.RecordCount = 1 Then
        sql3 = "INSERT INTO NGSPanelGenes ( NGSPanelID, symbol, HGNCID) VALUES (" & Me![NGSPanelID] & ", '" & Me![GeneSymbol] & "', '" & rs!HGNCID & "')"
        DoCmd.SetWarnings False
        DoCmd.RunSQL sql3
        DoCmd.SetWarnings True
        Me![GenePanelListBox].Value = Null
        Me![GenePanelListBox].Requery
        Me![GeneSymbol] = Null
    Else
        MsgBox "More than one gene found with that symbol. Could not add to panel", vbCritical, "ERROR"
    End If
    Set rs = Nothing
End Sub

Private Sub removeGene_Click()
    Dim sql4 As String
    'If no gene is selected, display error message
    If IsNull(Me![GenePanelListBox].Value) Then
        MsgBox "No gene selected.", vbExclamation, "No Gene Selected"
    'Delete gene from panel and update list box
    Else
        sql4 = "DELETE FROM NGSPanelGenes WHERE NGSPanelGenesID = " & Me![GenePanelListBox].Value
        DoCmd.SetWarnings False
        DoCmd.RunSQL sql4
        DoCmd.SetWarnings True
        Me![GenePanelListBox].Value = Null
        Me![GenePanelListBox].Requery
    End If
End Sub

Private Sub ExtendedPanelCombo_NotInList(NewData As String, Response As Integer)
    'Replaces default error message if referal type not in list
    MsgBox "Gene panel not recognised. Please select from the dropdown list", vbExclamation, "Select Extended Gene Panel"
    Response = acDataErrContinue
End Sub

Private Sub ExtendedPanelCombo_AfterUpdate()
    'Update extended panel code and name fields after selection
    Me![ExtendedPanelCode] = Me![ExtendedPanelCombo].Column(1)
    Me![ExtendedPanelName] = Me![ExtendedPanelCombo].Column(2)

    'Update Extended Panel listbox
    Me![ExtPanListbox].Requery
End Sub

Private Sub SubmitButton_Click()
    Dim sql4 As String
    Dim sql5 As String
    Dim d As String
    Dim un As String
    Dim cn As String

    'Add Extended NGSPanelID to ExomeTest record
    If Not IsNull(Me![ExtendedPanelCombo]) Then
        sql4 = "UPDATE ExomeTest SET ExtendedPanelID = " & Me![ExtendedPanelCombo] & " WHERE ExomeTestID = " & ExomeTestID
        DoCmd.SetWarnings False
        DoCmd.RunSQL sql4
        DoCmd.SetWarnings True
    End If

    'Insert record of Exome test request into patient log
    d = Format(Now(), "dd/mmm/yyyy Hh:Nn:ss")
    un = VBA.Environ("USERNAME")
    cn = VBA.Environ("COMPUTERNAME")
    sql5 = "INSERT INTO PatientLog(InternalPatientID, LogEntry, [Date], Login, PCName) VALUES(" & internalPatID & ", 'NGS: " & Referral & " test request added. Main panel: " & Me![PanelCode] & ". Extended panel: " & Me![ExtendedPanelCode] & "',#" + d + "#,'" + un + "','" + cn + "')"
    DoCmd.SetWarnings False
    DoCmd.RunSQL sql5
    DoCmd.SetWarnings True

    'Display message and close form
    MsgBox "Exome test request submitted." & vbNewLine & vbNewLine & "Pending authorisation.", vbInformation, "Test Request Submitted"
    DoCmd.Close
End Sub
