Option Compare Database

Private Sub btnClose_Click()
  DoCmd.Close
End Sub

Private Sub btnFindPatient_Click()
    Me![ListPatientFound].Requery
    If Me![ListPatientFound].ListCount = 0 Then
    msga = MsgBox("PRU not found in Geneworks.", 0, "PRU not found")
    End If
    Me![txFamilyTrustID].SetFocus
End Sub

Private Sub Form_Open(Cancel As Integer)
    Me![txFamilyTrustID].SetFocus
End Sub

Private Sub ListPatientFound_DblClick(Cancel As Integer)
    'Declare variables
    Dim moka_patients As ADODB.Recordset
    Set moka_patients = New ADODB.Recordset
    Dim rsAddPatient As ADODB.Recordset
    Set rsAddPatient = New ADODB.Recordset
    Dim rsAddNGSTest As ADODB.Recordset
    Set rsAddNGSTest = New ADODB.Recordset
    Dim usr As String
    Dim dt As String
    Dim cmp As String
    Dim strReadMoka As String
    Dim internal_patient_id As String
    Dim strAddPatientSQL As String
    Dim strAddNGSTest As String
    
    ' set time, user and computer names for logs
    dt = Format(Now, "dd/mmm/yyyy Hh:Nn:ss")
    usr = VBA.Environ("USERNAME")
    cmp = VBA.Environ("COMPUTERNAME")
    
    ' This list box lists any patients in GW with this family ID. This code is run when a single family member is clicked
    ' Me.ListPatientFound.Value is the PRU for that individual
    ' Step 1. Check if already in patients table - get internal patient id
    ' Step 2. If not insert to patients table from geneworks- get internal patient id
    ' Step 3. Create NGS test with internal patient id
    ' Step 4. Display the relevant info for the NGSTest in subform
    
    
    ' Step 1. Check if already in patients table - get internal patient id
    ' pull out InternalPatientID from patients table
    strReadMoka = "SELECT Patients.InternalPatientID, Patients.PatientID From Patients where Patients.PatientID = '" & Me.ListPatientFound.Value & "'"
    
    ' Recordset with any existing entries in patients
    moka_patients.Open strReadMoka, CurrentProject.Connection, adOpenKeyset
    
    ' Test that a patient has been selected
    If Not IsNull(Me.ListPatientFound.Value) Then
        ' If there is no record in patients
        If moka_patients.RecordCount = 0 Then
            ' insert to moka
            strAddPatientSQL = "INSERT INTO Patients(PatientID, s_StatusOverall, MokaCreated, MokaCreatedBy, MokaCreatedPC, NHSNumber, BookinLastname, BookinFirstName, BookinDoB, BookinCreatedBy, BookinCreatedDate, BookinSex) SELECT '" & Me.ListPatientFound.Value & "', 1202218839, now(),'" & usr & "','" & cmp & "', dbo_patientlinked.NHSNo, dbo_patientlinked.LastName, dbo_patientlinked.FirstName, dbo_patientlinked.DOB, dbo_patientlinked.CreatedByID, dbo_patientlinked.CreatedDate, dbo_patientlinked.Gender from dbo_patientlinked where patienttrustid = '" & Me.ListPatientFound.Value & "'"
            Debug.Print strAddPatientSQL
            
            'execute the query
            rsAddPatient.Open strAddPatientSQL, CurrentProject.Connection, adOpenKeyset
            ' select the internal patient id created from the insert
            rsAddPatient.Open "SELECT @@identity", CurrentProject.Connection, adOpenKeyset
            ' assign to variable
            internal_patient_id = rsAddPatient.Fields(0).Value
            ' update status box on form
            Me.what_has_happened.Value = "Patient record inserted into Patients table"
        ' if there are multiple records for that PRU
        ElseIf moka_patients.RecordCount > 1 Then
            ' open a message box warning of this
            'msgbx = MsgBox("Error - more than one records in patients table for PRU " & Me.ListPatientFound.Value, "Error")
            ' update status box on form
            Me.what_has_happened.Value = "Error - more than one record (" & moka_patients.RecordCount & ")in patients table for PRU " & Me.ListPatientFound.Value
            'STOP code from running any further
            Exit Sub
        ' If there is a matching record in Moka
        Else
        ' capture the internal_patient_id  ready to insert to NGSTest
        internal_patient_id = moka_patients!InternalPatientID
        ' update status box on form
        Me.what_has_happened.Value = "Patient record found in Patients table"
    End If
    End If
    
    
    ' Now we have the internal_patient_id  we can insert to the NGSTestTable
    ' only want to create a ngstest once = need to check if it has already been added - use service and internal patient id
    strAddNGSTest = "select * from NGSTest where service = 0 and InternalPatientID = " & internal_patient_id
    ' execute the select query
    rsAddNGSTest.Open strAddNGSTest, CurrentProject.Connection, adOpenKeyset
    ' if there is no results
    If rsAddNGSTest.RecordCount = 0 Then
        ' empty the record set
        Set rsAddNGSTest = Nothing
        ' reset as recordset
        Set rsAddNGSTest = New ADODB.Recordset
        ' referralID and statusID = 100K from status and referral tables, bookby and BookingAuthorisedByID = NA - these will be assigned in the subform
        strAddNGSTest = "insert into NGSTest (InternalPatientID,ReferralID,StatusID,DateRequested,BookBy,BookingAuthorisedByID,Service) Values (" & internal_patient_id & ",1199901218,1202218839, now(),1201865434,1201865434,0)"
        ' execute insert statement
        rsAddNGSTest.Open strAddNGSTest, CurrentProject.Connection, adOpenKeyset
        Me.what_has_happened.Value = Me.what_has_happened.Value & vbNewLine & "NGStest created"
    End If
    Me.what_has_happened.Value = Me.what_has_happened.Value & vbNewLine & "NGStest already present"
    ' populate the subform record source
    Me.s88005_subform.Form.RecordSource = "SELECT NGSTest.NGSTestID, Patients.BookinLastName, Patients.BookinFirstName, Patients.BookinSex, Patients.BookinDOB, GELProbandID, IRID, BookBy, BookingAuthorisedByID FROM NGSTest INNER JOIN Patients ON NGSTest.InternalPatientID = Patients.InternalPatientID where Patients.InternalPatientID = " & internal_patient_id
    ' requery the subform
    Me.s88005_subform.Requery
End Sub
Private Sub refresh_form_Click()
On Error GoTo Err_refresh_form_Click
    ' reset all the recordsources/values to nothing ane then refresh the form
    Me![txFamilyTrustID] = Nothing
    Me![ListPatientFound].Requery
    Me.what_has_happened.Value = Nothing
    ' reset recordsource using giving an internalpatientid of 0 to give an empty form
    Me.s88005_subform.Form.RecordSource = "SELECT NGSTest.NGSTestID, Patients.BookinLastName, Patients.BookinFirstName, Patients.BookinSex, Patients.BookinDOB, GELProbandID, IRID, BookBy, BookingAuthorisedByID FROM NGSTest INNER JOIN Patients ON NGSTest.InternalPatientID = Patients.InternalPatientID where Patients.InternalPatientID = 0"
    ' refresh form
    Me.Refresh
    'set focus ready for next sample
    Me![txFamilyTrustID].SetFocus

Exit_refresh_form_Click:
    Exit Sub

Err_refresh_form_Click:
    MsgBox Err.Description
    Resume Exit_refresh_form_Click
    
End Sub
