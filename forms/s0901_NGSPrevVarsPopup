Option Compare Database
Private Sub Form_Load()
    Dim rsTranHGVS As ADODB.Recordset
    Dim pos2us As Long
    Dim pos2ds As Long
    Dim sqlPrevVars As String
    Dim sqlCurrentVar As String
    Dim sqlProtMatchVars As String
    
    'Clear any previous row source queries from prev vars phenotype listbox
    Me.ListboxPhenoPrev.RowSource = "SELECT Null"
       
    'Create recordset of each transcript and HGVS protein change for the current variant
    sqlTranHGVS = "SELECT NGSVariantAnnotations.TRANSCRIPT_ID, NGSVariantAnnotations.HGVS_Protein FROM NGSVariant INNER JOIN NGSVariantAnnotations ON NGSVariant.NGSVariantID = NGSVariantAnnotations.NGSVariantID WHERE NGSVariant.NGSVariantID = " & Me.NGSVariantID & " AND NGSVariantAnnotations.TRANSCRIPT_ID IS NOT NULL AND NGSVariantAnnotations.HGVS_Protein IS NOT NULL"
    Set rsTranHGVS = New ADODB.Recordset
    rsTranHGVS.Open sqlTranHGVS, CurrentProject.Connection, adOpenKeyset
    'If there is at lease one protein change associated with this variant, find all other variants that have any matching protein changes
    If rsTranHGVS.RecordCount > 0 Then
        sqlProtMatchVars = "SELECT DISTINCT NGSVariant.NGSVariantID FROM NGSVariant INNER JOIN NGSVariantAnnotations ON NGSVariant.NGSVariantID = NGSVariantAnnotations.NGSVariantID WHERE "
        'loop through recordset and add each transcipt + protein change combination to the where clause so matching variants are pulled out.
        While Not rsTranHGVS.EOF
            sqlWhere = sqlWhere + "(NGSVariantAnnotations.TRANSCRIPT_ID = '" & rsTranHGVS.Fields("TRANSCRIPT_ID") & "' AND NGSVariantAnnotations.HGVS_Protein = '" & rsTranHGVS.Fields("HGVS_Protein") & "')"
            If rsTranHGVS.AbsolutePosition <> rsTranHGVS.RecordCount Then
                sqlWhere = sqlWhere + " OR "
            End If
            rsTranHGVS.MoveNext
        Wend
        sqlProtMatchVars = sqlProtMatchVars + sqlWhere
    End If
    Set rsTranHGVS = Nothing
        
    'get upstream and downstream positions
    pos2us = Me.Position_hg19 - 2 '2 bp downstream of the variant position
    pos2ds = Me.Position_hg19 + Len(Me.ref) + 1 '2 bp downstream from the end of the ref allele
    
    'build record source query for prevVars subform
    'First fields to be pulled in
    sqlPrevVars = "SELECT NGSVariant.*, Chromosome.Chr, Patients.PatientID, NGSVariantComment.Comment, NGSVariantACMG.NGSVariantACMGID"
    'Next, determine whether each variant pulled in is a 'match' (i.e. exactly the same variant or causes the same protein change in any of the transcripts)
    'Following line checks if variant is an exact match (regardless of whether it's protein coding)
    sqlPrevVars = sqlPrevVars & ", IIF((NGSVariant.ChrID = " & Me.ChrID & " AND NGSVariant.Position_hg19 = " & Me.Position_hg19 & " AND NGSVariant.ref = '" & Me.ref & "' AND NGSVariant.alt = '" & Me.Alt & "')"
    'Following lines find variants that cause the same protein change. First checks a string has been created for sqlProtMatchVars (i.e. if there is at least one protein change associated with variant).
    If sqlProtMatchVars <> "" Then
        sqlPrevVars = sqlPrevVars + " OR NGSVariant.NGSVariantID IN (" & sqlProtMatchVars & ")"
    End If
    'If either of the above conditions are met, add 'yes' to the exact match column, else, add 'no'.
    sqlPrevVars = sqlPrevVars + ", 'Yes', 'No') As matchVar FROM (((NGSVariant INNER JOIN Chromosome ON NGSVariant.ChrID = Chromosome.ChrID) INNER JOIN Patients ON NGSVariant.InternalPatientID = Patients.InternalPatientID) LEFT JOIN NGSVariantComment ON NGSVariant.NGSVariantCommentID = NGSVariantComment.NGSVariantCommentID) LEFT JOIN NGSVariantACMG ON NGSVariant.NGSVariantID = NGSVariantACMG.NGSVariantID WHERE NGSVariant.NGSVariantID <> " & Me.NGSVariantID & " AND NGSVariant.ChrID = " & Me.ChrID & " AND NGSVariant.Position_hg19 BETWEEN " & pos2us & " AND " & pos2ds & " ORDER BY 1, ABS(NGSVariant.Position_hg19 - " & Me.Position_hg19 & "), NGSVariant.alt"
  
    'Update record source of NGSPrevVar subform
    [Form_s0901_NGSPrevVars].RecordSource = sqlPrevVars
    [Form_s0901_NGSPrevVars].Requery 'Reports that occassionally wrong variant is displayed. Not been able to recreate problem so trying a requery here to see if it fixes it??
End Sub
