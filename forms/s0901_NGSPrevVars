Private Sub Form_Current()
    Me.Parent.ListboxPhenoPrev.RowSource = "SELECT Phenotype FROM Phenotype WHERE InternalPatientID = " & Me.InternalPatientID
End Sub

Private Sub s0901_NGSVariantsAnnotations_Enter()
    Me.Parent.ListboxPhenoPrev.RowSource = "SELECT Phenotype FROM Phenotype WHERE InternalPatientID = " & Me.InternalPatientID
End Sub

Private Sub commentShort_DblClick(Cancel As Integer)
    Dim stDocName As String
    Dim rsAddComment As ADODB.Recordset
    Dim sqlAddComment As String
    Dim requiresRequery As Boolean
    
    'Initialise requiresRequery to False. If a new comment/acmg record is created, a requery of the form is needed to pull in the new records
    'However this has to be done at the end otherwise the form resets to the first record and the wrong variant is opened in the ACMG form
    requiresRequery = False
       
    'If there isn't a comment, create an empty one.
    If IsNull(Me.NGSVariantCommentID) Then
        Set rsAddComment = New ADODB.Recordset ' create empty recordset to add new comment
        sqlAddComment = "INSERT INTO NGSVariantComment(Comment) VALUES ('')" ' SQL query to insert an empty comment into NGSVariantComment table
        rsAddComment.Open sqlAddComment, CurrentProject.Connection, adOpenKeyset ' execute the query to add empty comment
        ' capture the NGSVariantCommentID for the newly created empty comment. Store in commentID variable.
        rsAddComment.Open "SELECT @@identity", CurrentProject.Connection, adOpenKeyset
        commentID = rsAddComment.Fields(0).Value
        Set rsAddComment = Nothing ' Close the recordset to close database connection
        Me.NGSVariantCommentID = commentID ' set the commentID for this record to the key for the newly created empty comment
        requiresRequery = True 'Set requiresRequery to True so requery is performed at end
    End If
    
    'If there isn't an existing ACMG record for the variant, create an empty one
    If IsNull(Me.NGSVariantACMGID) Then
        Set rsAddACMG = New ADODB.Recordset ' create new recordset
        ' sql query to insert NGSVariantID into NGSVariantACMG table
        sqlAddACMG = "INSERT INTO NGSVariantACMG(NGSVariantID) VALUES (" & Me.NGSVariantID & ")"
        rsAddACMG.Open sqlAddACMG, CurrentProject.Connection, adOpenKeyset 'execute the query
        Set rsAddACMG = Nothing ' Close the recordset to close database connection
        requiresRequery = True 'Set requiresRequery to True so requery is performed at end
    End If
    'Needs a refresh for newly inserted comment/ACMG records to be found
    Me.Refresh
    stDocName = "s0901_NGSVariantACMGPopup" ' define name of the form to open
    stLinkCriteria = "[NGSVariantID] = " & Me.NGSVariantID ' Link the two forms on NGSVariantID
    'Open the ACMG window in READ ONLY mode
    DoCmd.OpenForm stDocName, , , stLinkCriteria, acFormReadOnly
    'Requery only if required (to prevent unnecessarily reseting to first record
    If requiresRequery Then
        Me.Requery
    End If
End Sub

Private Sub DateAdded_DblClick(Cancel As Integer)
    'View log history for Variant
    Me.Dirty = False 'Forces any changes to be saved
    Dim stDocName As String
    stDocName = "s0901_NGSLogMemo" 'Form to be opened
    ' Type = 2 just retrieves records relating the variant, LinkID links the two forms on NGSVariantID
    stLinkCriteria = "[Type] = 2 AND [LinkID] = " & Me.NGSVariantID
    DoCmd.OpenForm stDocName, acFormDS, , stLinkCriteria 'Open the form
End Sub
